# Copyright 2016 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

declare_args() {
  # DEPRECATED: The below variables are deprecated.
  # Instead use a config json as described in README.md.

  # if false, bypasses starting the MI Dashboard
  mi_dashboard = true
}

source_set("user") {
  deps = [
    ":maxwell_main",
  ]
}

executable("maxwell_main") {
  output_name = "maxwell"

  sources = [
    "maxwell_main.cc",
  ]

  deps = [
    ":config",
    ":user_intelligence_provider",
    "//application/lib/app",
    "//apps/maxwell/services/user",
    "//lib/mtl",
    "//third_party/rapidjson",
  ]
}

source_set("config") {
  sources = [
    "config.h",
  ]
}

source_set("user_intelligence_provider") {
  sources = [
    "user_intelligence_provider_impl.cc",
    "user_intelligence_provider_impl.h",
  ]

  public_deps = [
    "//application/lib/app",
  ]

  deps = [
    ":agent_launcher",
    ":config",
    ":intelligence_services",
    "//apps/maxwell/services/action_log",
    "//apps/maxwell/services/context",
    "//apps/maxwell/services/context:debug",
    "//apps/maxwell/services/resolver",
    "//apps/maxwell/services/suggestion",
    "//apps/maxwell/services/suggestion:debug",
    "//apps/maxwell/services/user",
    "//apps/maxwell/services/user:scope",
    "//apps/maxwell/src/acquirers/story_info:initializer",
    "//apps/network/services",
  ]

  defines = []

  if (mi_dashboard) {
    defines += [ "DEPRECATED_MI_DASHBOARD" ]
  }
}

source_set("intelligence_services") {
  sources = [
    "intelligence_services_impl.cc",
    "intelligence_services_impl.h",
  ]

  deps = [
    "//apps/maxwell/services/action_log",
    "//apps/maxwell/services/context",
    "//apps/maxwell/services/suggestion",
    "//apps/maxwell/services/user",
    "//apps/maxwell/services/user:scope",
  ]
}

source_set("agent_launcher") {
  sources = [
    "agent_launcher.cc",
    "agent_launcher.h",
  ]

  public_deps = [
    "//application/lib/app",
  ]

  deps = [
    "//apps/maxwell/src:application_environment_host_impl",
  ]
}
