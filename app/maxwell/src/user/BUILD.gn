# Copyright 2016 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

declare_args() {
  # DEPRECATED: The below variables are deprecated.
  # Instead use a config json as described in README.md.

  # set to true to bypass mi_dashboard launch.
  deprecated_no_mi_dashboard = false
}

source_set("user") {
  deps = [
    ":maxwell_main",
  ]
}

executable("maxwell_main") {
  output_name = "maxwell_main"

  sources = [
    "maxwell_main.cc",
  ]

  deps = [
    ":config",
    ":user_intelligence_provider",
    "//garnet/public/lib/app/cpp",
    "//apps/maxwell/services/user",
    "//garnet/public/lib/mtl",
    "//third_party/rapidjson",
  ]
}

source_set("config") {
  sources = [
    "config.cc",
    "config.h",
  ]
}

source_set("user_intelligence_provider") {
  sources = [
    "user_intelligence_provider_impl.cc",
    "user_intelligence_provider_impl.h",
  ]

  public_deps = [
    "//garnet/public/lib/app/cpp",
  ]

  deps = [
    ":agent_launcher",
    ":config",
    ":intelligence_services",
    "//apps/cobalt_client/services:cobalt",
    "//apps/maxwell/services/action_log",
    "//apps/maxwell/services/context",
    "//apps/maxwell/services/resolver",
    "//apps/maxwell/services/suggestion",
    "//apps/maxwell/services/suggestion:debug",
    "//apps/maxwell/services/user",
    "//apps/maxwell/services/user:scope",
    "//apps/maxwell/src/acquirers/story_info:initializer",
    "//garnet/public/lib/network/fidl",
  ]

  defines = []

  if (deprecated_no_mi_dashboard) {
    defines += [ "DEPRECATED_NO_MI_DASHBOARD" ]
  }
}

source_set("intelligence_services") {
  sources = [
    "intelligence_services_impl.cc",
    "intelligence_services_impl.h",
  ]

  deps = [
    "//apps/maxwell/services/action_log",
    "//apps/maxwell/services/context",
    "//apps/maxwell/services/suggestion",
    "//apps/maxwell/services/user",
    "//apps/maxwell/services/user:scope",
  ]
}

source_set("agent_launcher") {
  sources = [
    "agent_launcher.cc",
    "agent_launcher.h",
  ]

  public_deps = [
    "//garnet/public/lib/app/cpp",
  ]

  deps = [
    "//apps/maxwell/src:application_environment_host_impl",
  ]
}
