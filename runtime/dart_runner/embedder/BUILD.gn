# Copyright 2016 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/dart/dart_library.gni")
import("//build/dart/toolchain.gni")
import("//topaz/public/dart-pkg/fuchsia/sdk_ext.gni")
import("//topaz/public/dart-pkg/zircon/sdk_ext.gni")

if (is_debug) {
  _gen_snapshot_exe = "gen_snapshot"
} else {
  _gen_snapshot_exe = "gen_snapshot_product"
}

_gen_snapshot_label =
    "//third_party/dart/runtime/bin:$_gen_snapshot_exe($host_toolchain)"
_gen_snapshot_dir = get_label_info(_gen_snapshot_label, "root_out_dir")
_gen_snapshot = "$_gen_snapshot_dir/$_gen_snapshot_exe"

action("generate_dart_aot_snapshot_assembly") {
  snapshot_assembly = "$target_gen_dir/aot_vm_snapshot.S"

  deps = [
    _gen_snapshot_label,
  ]

  outputs = [
    snapshot_assembly,
  ]

  script = _gen_snapshot
  args = [
    # TODO(https://github.com/dart-lang/sdk/issues/32608): Default flags.
    "--limit_ints_to_64_bits",
    "--reify_generic_functions",
    "--strong",
    "--sync-async",
    "--snapshot_kind=vm-aot-assembly",
    "--assembly=" + rebase_path(snapshot_assembly),
  ]
  if (is_debug) {
    args += [ "--enable_asserts" ]
  }
}

source_set("dart_aot_snapshot_cc") {
  deps = [
    ":generate_dart_aot_snapshot_assembly",
  ]

  sources = [
    "$target_gen_dir/aot_vm_snapshot.S",
    "snapshot.h",
  ]
}
