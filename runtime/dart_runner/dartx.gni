# Copyright 2016 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

template("dartx") {
  if (defined(invoker.output_name)) {
    bundle_name = invoker.output_name
  } else {
    bundle_name = "${target_name}.dartx"
  }

  bundle_path = "$root_out_dir/$bundle_name"
  snapshot_path = "$target_gen_dir/${target_name}_snapshot.bin"
  depfile_path = "${snapshot_path}.d"

  main_dart = invoker.main_dart

  if (defined(invoker.packages)) {
    packages_path = invoker.packages
  } else {
    main_dir = get_path_info(main_dart, "dir")
    packages_path = "$main_dir/.packages"
  }

  dart_snapshotter_label = "//apps/dart_content_handler/snapshotter($host_toolchain)"
  dart_snapshotter_dir =
      get_label_info(dart_snapshotter_label, "root_out_dir")
  dart_snapshotter = "$dart_snapshotter_dir/dart_snapshotter"

  action(target_name) {
    depfile = depfile_path

    inputs = [
      main_dart,
      packages_path,
    ]

    outputs = [
      bundle_path,
      snapshot_path,
    ]

    if (defined(invoker.sources)) {
      sources = invoker.sources
    }

    script = dart_snapshotter

    rebased_packages_path = rebase_path(packages_path)
    rebased_bundle_path = rebase_path(bundle_path)
    rebased_snapshot_path = rebase_path(snapshot_path)
    rebased_depfile_path = rebase_path(depfile_path)
    rebased_build_output_path = rebase_path(bundle_path, root_build_dir)

    args = [
      "--packages=$rebased_packages_path",
      "--dartx=$rebased_bundle_path",
      "--snapshot=$rebased_snapshot_path",
      "--depfile=$rebased_depfile_path",
      "--build-output=$rebased_build_output_path",
      rebase_path(main_dart),
    ]

    deps = [
      dart_snapshotter_label,
    ]

    if (defined(invoker.deps)) {
      deps += invoker.deps
    }
  }
}
