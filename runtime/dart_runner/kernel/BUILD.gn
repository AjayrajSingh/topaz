# Copyright 2018 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/dart/dart_tool.gni")
import("//third_party/dart/utils/compile_platform.gni")

compile_platform("non_strong_platform") {
  sources = [
    "libraries.json",
  ]

  outputs = [
    "$root_out_dir/runner_patched_sdk/platform.dill",
    "$root_out_dir/runner_patched_sdk/vm_outline.dill",
  ]

  args = [
    "--target=runner",
    "dart:core",
  ]
}

compile_platform("strong_platform") {
  sources = [
    "libraries.json",
  ]

  outputs = [
    "$root_out_dir/runner_patched_sdk/platform_strong.dill",
    "$root_out_dir/runner_patched_sdk/vm_outline_strong.dill",
  ]

  args = [
    "--target=runner",
    "--strong",
    "dart:core",
  ]
}

group("kernel_platform_files") {
  public_deps = [
    ":non_strong_platform",
    ":strong_platform",
  ]
}

action("kernel_core_snapshot") {
  if (is_debug) {
    gen_snapshot_exe = "gen_snapshot"
  } else {
    gen_snapshot_exe = "gen_snapshot_product"
  }

  gen_snapshot_label =
      "//third_party/dart/runtime/bin:$gen_snapshot_exe($host_toolchain)"
  gen_snapshot_dir = get_label_info(gen_snapshot_label, "root_out_dir")

  deps = [
    ":kernel_platform_files",
    gen_snapshot_label,
  ]

  platform_dill = "$root_out_dir/runner_patched_sdk/platform_strong.dill"
  inputs = [
    platform_dill,
  ]

  vm_snapshot_data = "$target_gen_dir/vm_isolate_snapshot.bin"
  vm_snapshot_instructions = "$target_gen_dir/vm_snapshot_instructions.bin"
  isolate_snapshot_data = "$target_gen_dir/isolate_snapshot.bin"
  isolate_snapshot_instructions =
      "$target_gen_dir/isolate_snapshot_instructions.bin"
  outputs = [
    vm_snapshot_data,
    vm_snapshot_instructions,
    isolate_snapshot_data,
    isolate_snapshot_instructions,
  ]

  script = "//third_party/dart/runtime/tools/create_snapshot_bin.py"
  args = [
    "--executable",
    rebase_path("$gen_snapshot_dir/$gen_snapshot_exe"),
    "--script",
    rebase_path(platform_dill),
    "--snapshot_kind",
    "core",
    "--vm_flag",
    "--await_is_keyword",
    "--vm_flag",
    "--enable_mirrors=false",
    "--vm_output_bin",
    rebase_path(vm_snapshot_data, root_build_dir),
    "--vm_instructions_output_bin",
    rebase_path(vm_snapshot_instructions, root_build_dir),
    "--isolate_output_bin",
    rebase_path(isolate_snapshot_data, root_build_dir),
    "--isolate_instructions_output_bin",
    rebase_path(isolate_snapshot_instructions, root_build_dir),
  ]
}

dart_tool("kernel_compiler") {
  main_dart = "compiler.dart"
  source_dir = "."
  disable_analysis = true

  deps = [
    "//third_party/dart-pkg/pub/args",
    "//third_party/dart/pkg/front_end",
    "//third_party/dart/pkg/kernel",
    "//third_party/dart/pkg/vm",
  ]
}
