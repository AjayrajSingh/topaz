# Copyright 2018 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/dart/dart.gni")
import("//build/dart/dart_tool.gni")

dart_tool("kernel_compiler") {
  main_dart = "compiler.dart"
  source_dir = "."

  deps = [
    "//third_party/dart-pkg/pub/args",
    "//third_party/dart/pkg/front_end",
    "//third_party/dart/pkg/kernel",
    "//third_party/dart/pkg/vm",
  ]
}

# TODO(alainv): Move that to dart_tool.gni to make it more generic.
sdk_atom("kernel_compiler_sdk") {
  domain = "dart"
  name = "kernel_compiler"
  category = "partner"

  tags = [ "type:tool" ]

  dart_target_gen_dir =
      get_label_info(":bogus($host_toolchain)", "target_gen_dir")
  kernel_compiler_path = "$dart_target_gen_dir/kernel_compiler.snapshot"

  files = [
    {
      source = "$kernel_compiler_path"
      dest = "dart_prebuilts/kernel_compiler.snapshot"
    },
    {
      source = "$dart_sdk/bin/dart"
      dest = "dart"
    },
    {
      source = "$root_out_dir/dart_runner_patched_sdk/platform_strong.dill"
      dest = "dart_prebuilts/dart_runner/platform_strong.dill"
    },
    {
      source = "$root_out_dir/flutter_runner_patched_sdk/platform_strong.dill"
      dest = "dart_prebuilts/flutter_runner/platform_strong.dill"
    },
  ]

  non_sdk_deps = dart_sdk_deps + [
    ":kernel_compiler_snapshot($host_toolchain)",
    "//topaz/runtime/dart_runner/kernel:kernel_platform_files",
    "//topaz/runtime/flutter_runner/kernel:kernel_platform_files",
  ]
}
