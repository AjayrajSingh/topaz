# Copyright 2016 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//packages/package.gni")

declare_args() {
  use_prebuilt_webkit = true
}

executable("web_view_bin") {
  output_name = "web_view"

  sources = [
    "main.cpp",
  ]

  if (use_prebuilt_webkit) {
    public_deps = [ ":prebuilt_webkit" ]
  } else {
    public_deps = [ "//third_party/webkit" ]
  }

  deps = [
    "//application/lib/app",
    "//apps/icu_data/lib",
    "//apps/modular/lib/rapidjson",
    "//apps/modular/services/module",
    "//apps/modular/services/story",
    "//apps/mozart/lib/view_framework:view_provider",
    "//apps/mozart/services/buffers/cpp",
    "//apps/mozart/services/composition",
    "//apps/mozart/services/geometry",
    "//apps/mozart/services/views",
    "//apps/web_runner/services",
    "//lib/ftl",
    "//lib/mtl",
    "//third_party/boringssl",
    "//third_party/rapidjson",
  ]

  ldflags = [ "-Wl,-z,stack-size=1048576" ]
}

if (use_prebuilt_webkit) {
  group("prebuilt_webkit") {
    public_deps = [
      ":copy_prebuilt_webkit_so",
      ":prebuilt_webkit_shared_deps(//build/toolchain/fuchsia:${target_cpu}-shared)",
    ]

    public_configs = [ ":prebuilt_webkit_config" ]
  }

  config("prebuilt_webkit_config") {
    include_dirs = [ "//third_party/webkit/Source/WebKit/fuchsia" ]
    ldflags = [ "x64-shared/libwebkit.so" ]
  }

  copy("copy_prebuilt_webkit_so") {
    if (target_cpu == "x64") {
      arch = "x86_64"
    } else if (target_cpu == "arm64") {
      arch = "aarch64"
    } else {
      assert(false, "Target architecture not supported")
    }
    sources = [
      "prebuilt/$arch/libwebkit.so",
    ]
    outputs = [
      "$root_out_dir/x64-shared/libwebkit.so",
    ]
  }

  group("prebuilt_webkit_shared_deps") {
    deps = [
      "//third_party/boringssl:ssl",
      "//third_party/cairo",
      "//third_party/curl:libcurl",
      "//third_party/freetype2",
      "//third_party/harfbuzz",
      "//third_party/icu:icui18n",
      "//third_party/icu:icuuc",
      "//third_party/libjpeg-turbo:libjpeg",
      "//third_party/libpng",
      "//third_party/libxml2",
      "//third_party/sqlite",
      "//third_party/zlib",
    ]
  }
}

package("web_view") {
  deps = [
    ":web_view_bin",
  ]

  binaries = [ {
        name = "web_view"
      } ]

  if (target_cpu == "x64") {
    libraries = [ {
          name = "libwebkit.so"
        } ]
  }

  resources = [ {
        path = rebase_path("//apps/fonts/third_party/roboto/Roboto-Regular.ttf")
        dest = "data/webkit/Roboto-Regular.ttf"
      } ]
}
