# Copyright 2018 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/dart/dart_tool.gni")
import("//third_party/dart/utils/compile_platform.gni")

compile_platform("kernel_platform_files") {
  sources = [
    "libraries.json",
  ]

  outputs = [
    "$root_out_dir/flutter_runner_patched_sdk/platform_strong.dill",
    "$root_out_dir/flutter_runner_patched_sdk/vm_outline_strong.dill",
  ]

  args = [
    "--target=flutter_runner",
    "--strong",
    "dart:core",
  ]
}

action("kernel_core_snapshot") {
  deps = gen_snapshot_deps + [
    ":kernel_platform_files",
  ]

  platform_dill =
      "$root_out_dir/flutter_runner_patched_sdk/platform_strong.dill"
  compilation_trace = "//topaz/runtime/flutter_runner/compilation_trace.txt"
  inputs = [
    platform_dill,
    compilation_trace,
  ]

  vm_snapshot_data = "$target_gen_dir/vm_isolate_snapshot.bin"
  vm_snapshot_instructions = "$target_gen_dir/vm_snapshot_instructions.bin"
  isolate_snapshot_data = "$target_gen_dir/isolate_snapshot.bin"
  isolate_snapshot_instructions =
      "$target_gen_dir/isolate_snapshot_instructions.bin"
  outputs = [
    vm_snapshot_data,
    vm_snapshot_instructions,
    isolate_snapshot_data,
    isolate_snapshot_instructions,
  ]

  script = gen_snapshot
  args = [
    # TODO(https://github.com/dart-lang/sdk/issues/32608): Default flags.
    "--limit_ints_to_64_bits",
    "--reify_generic_functions",
    "--strong",
    # Re-enable once enabled in //third_party/flutter/runtime/dart_vm.cc
    # "--sync-async",
    "--enable_mirrors=false",
    "--deterministic",
    "--snapshot_kind=core-jit",
    "--load_compilation_trace=" + rebase_path(compilation_trace, root_build_dir),
    "--vm_snapshot_data=" + rebase_path(vm_snapshot_data, root_build_dir),
    "--vm_snapshot_instructions=" +
        rebase_path(vm_snapshot_instructions, root_build_dir),
    "--isolate_snapshot_data=" +
        rebase_path(isolate_snapshot_data, root_build_dir),
    "--isolate_snapshot_instructions=" +
        rebase_path(isolate_snapshot_instructions, root_build_dir),
  ]
  if (is_debug) {
    args += [ "--enable_asserts" ]
  }
  args += [ rebase_path(platform_dill) ]
}
